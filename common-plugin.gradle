apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: "org.grails.grails-plugin"

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    documentation
    all { Configuration configuration->
        configuration.resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.apache.groovy' && details.requested.name.startsWith('groovy')) {
                details.useVersion(groovyVersion)
            }
        }
    }
}

dependencies {
    api "org.apache.groovy:groovy:$groovyVersion"
    api "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation "org.spockframework:spock-core:${spockVersion}", {
        exclude module:'groovy-all'
    }
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation "org.objenesis:objenesis:1.4"

    documentation "org.fusesource.jansi:jansi:1.14"

}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

// groovydoc.classpath += (configurations.documentation + configurations.compileClasspath)

groovydoc.doFirst {
    classpath += (configurations.documentation + configurations.compileClasspath)
}

bootJar.enabled = false

jar {
    enabled = true
    archiveClassifier.set('')
}
